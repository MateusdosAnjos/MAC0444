PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://www.semanticweb.org/root/ontologies/2019/10/projeto_mac0444#>


# Consulta 1.

	SELECT ?TituloDoFilme
	WHERE {
		VALUES ?diretor {:eric_schaeffer}.
		?Filme :dirigido_por ?diretor.
		?Filme :titulo ?TituloDoFilme.
	}
	ORDER BY ?TituloDoFilme


# Consulta 2.

	SELECT ?PrimeiroNome ?UltimoNome ?Personagem
	WHERE {
        VALUES ?titulo {'Her'}
        ?filme :titulo ?titulo.
        ?atuacao :filme_da_atuacao ?filme.
		?estrela :atua ?atuacao.
		?atuacao :personagem_da_atuacao ?Personagem.
		?estrela :primeiro_nome ?PrimeiroNome.
		?estrela :nome_familia ?UltimoNome.
		
	}
	ORDER BY ?PrimeiroNome ?UltimoNome


# Consulta 3.

	SELECT ?Filmes ?Diretor ?Ano
	WHERE {
		VALUES (?atorUm ?atorDois) {(:john_turturro :john_goodman)}.
		?atorUm :atua ?atuaUm.
		?atorDois :atua ?atuaDois.
		?atuaUm :filme_da_atuacao ?Filmes.
		?atuaDois :filme_da_atuacao ?Filmes.
		?Filmes :dirigido_por ?Diretor.
		?Filmes :ano_lancamento ?Ano.  
	}
	ORDER BY ?Ano


# Consulta 4 - scarlett_johansson aparece em todos os filmes 
# do diretor woody_allen, enquanto emily_mortimer aparece apenas em 1 deles.

	SELECT DISTINCT ?Filme ?Duracao
	WHERE {
		VALUES (?f ?X ?Y) {(:match_point :emily_mortimer :scarlett_johansson)}.
		?f :dirigido_por ?diretor.
		?Filme :dirigido_por ?diretor.
		?X :atua ?atuacaoX.
		?Y :atua ?atuacaoY.
        {?atuacaoX :filme_da_atuacao ?Filme.}
        UNION
        {?atuacaoY :filme_da_atuacao ?Filme.}
		?Filme :duracao ?Duracao.
	}	
	ORDER BY ?Duracao

# Consulta 4 - michael_patrick_bell não aparece em nenhum dos filmes 
# de woody_allen, já jody_halse aparece em apenas 1 deles.

	SELECT DISTINCT ?Filme ?Duracao
	WHERE {
		VALUES (?f ?X ?Y) {(:match_point :michael_patrick_bell :jody_halse)}.
		?f :dirigido_por ?diretor.
		?Filme :dirigido_por ?diretor.
		?X :atua ?atuacaoX.
		?Y :atua ?atuacaoY.
        {?atuacaoX :filme_da_atuacao ?Filme.}
        UNION
        {?atuacaoY :filme_da_atuacao ?Filme.}
		?Filme :duracao ?Duracao.
	}	
	ORDER BY ?Duracao


# Consulta 5 - Listando todas as combinações.

	SELECT ?Pessoa ?FilmeAt ?FilmeDir 
	WHERE {
        ?Pessoa rdf:type :Diretor.
        ?FilmeDir :dirigido_por ?Pessoa.
        ?Pessoa :atua ?atuacao.
        ?atuacao :filme_da_atuacao ?FilmeAt.
	}
    ORDER BY ?Pessoa

# Consulta 5 - Lista apenas um exemplo por Pessoa.

	SELECT ?Pessoa (SAMPLE(?filme_atuado) AS ?FilmeAt) (SAMPLE(?filme_dirigido) AS ?FilmeDir)
	WHERE {
        ?Pessoa rdf:type :Diretor.
        ?filme_dirigido :dirigido_por ?Pessoa.
        ?Pessoa :atua ?atuacao.
        ?atuacao :filme_da_atuacao ?filme_atuado.
	}
    GROUP BY ?Pessoa
    ORDER BY ?Pessoa


# Consulta 6 - Mostra que o filtro de Ano funciona, pois não exibe o filme
# barton_fink de 1991.

	SELECT ?Ano ?Diretor ?Filme
	WHERE {
        VALUES (?N1 ?N2) {(1994 2004)}
        VALUES (?X ?Y) {(:john_turturro :john_goodman)}.
        ?Filme :ano_lancamento ?Ano.
        FILTER (?Ano >= ?N1 && ?Ano <= ?N2).
        ?Filme :dirigido_por ?Diretor.
        ?X :atua ?atuacaoX.
        ?Y :atua ?atuacaoY.
        ?atuacaoX :filme_da_atuacao ?Filme.
        ?atuacaoY :filme_da_atuacao ?Filme.
	}	
    ORDER BY ?Ano

# Consulta 6 - Mostra que são capturados filmes de diretores diferentes.

	SELECT ?Ano ?Diretor ?Filme
	WHERE {
        VALUES (?N1 ?N2) {(1994 2004)}
        VALUES (?X ?Y) {(:issac_freeman :jerry_douglas)}.
        ?Filme :ano_lancamento ?Ano.
        FILTER (?Ano >= ?N1 && ?Ano <= ?N2).
        ?Filme :dirigido_por ?Diretor.
        ?X :atua ?atuacaoX.
        ?Y :atua ?atuacaoY.
        ?atuacaoX :filme_da_atuacao ?Filme.
        ?atuacaoY :filme_da_atuacao ?Filme.
	}	
    ORDER BY ?Ano

# Consulta 7 - Lista todas as combinações de pares (ator, atriz) para dois 
# filmes diferentes.

	SELECT ?Atriz ?Ator ?Filme ?Duracao
	WHERE {
        VALUES (?M1 ?M2) {(126 126)}
        FILTER (?Duracao >= ?M1 && ?Duracao <= ?M2).
        ?Filme :duracao ?Duracao.
        ?atuacaoAtor :filme_da_atuacao ?Filme.
        ?atuacaoAtriz :filme_da_atuacao ?Filme.
        ?Ator rdf:type :Ator.
        ?Ator :atua ?atuacaoAtor.
        ?Atriz rdf:type :Atriz.
        ?Atriz :atua ?atuacaoAtriz.
	}	
    ORDER BY ?Filme

# Consulta 7 - Mostra um exemplo menor, de apenas 1 filme.

	SELECT ?Atriz ?Ator ?Filme ?Duracao
	WHERE {
        VALUES (?M1 ?M2) {(149 149)}
        FILTER (?Duracao >= ?M1 && ?Duracao <= ?M2).
        ?Filme :duracao ?Duracao.
        ?atuacaoAtor :filme_da_atuacao ?Filme.
        ?atuacaoAtriz :filme_da_atuacao ?Filme.
        ?Ator rdf:type :Ator.
        ?Ator :atua ?atuacaoAtor.
        ?Atriz rdf:type :Atriz.
        ?Atriz :atua ?atuacaoAtriz.
	}	
    ORDER BY ?Filme

# Consulta 8.
	
	SELECT ?Diretor
	WHERE {
		SELECT ?Diretor (count(?Diretor) as ?count)
		WHERE {
			VALUES ?Xp {"Ewan"}.
			VALUES ?Xu {"McGregor"}.
			?x rdfs:subClassOf :Estrela.
			?ator rdf:type ?x.
			?ator :primeiro_nome ?Xp.
			?ator :nome_familia ?Xu.
			?ator :atua ?atuacao.
			?atuacao :filme_da_atuacao ?Filme.
			?Filme :dirigido_por ?Diretor.	
		}
		GROUP BY ?Diretor
		ORDER BY DESC (?count)
		LIMIT 1
	}

# Consulta 9.

	SELECT ?Filme ?Ano ?Personagem
	WHERE {
			VALUES ?ator {:scarlett_johansson}.
			?ator :atua ?atuacao.
            ?atuacao :filme_da_atuacao ?Filme.
            ?atuacao :personagem_da_atuacao ?Personagem.
            ?Filme :ano_lancamento ?Ano.
	}
    ORDER BY ?Ano
    LIMIT 1

# Consulta 10.

	SELECT ?Filme ?Duracao
	WHERE {
			VALUES ?diretor {:joel_coen}.
			?diretor :dirige ?Filme.
            ?Filme :duracao ?Duracao
	}	
    ORDER BY DESC (?Duracao)
    LIMIT 1
		